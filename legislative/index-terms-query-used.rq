SELECT DISTINCT
  ?house ?houseLabel
  ?legislature ?legislatureLabel
  ?term ?termLabel
  ?termStart ?termEnd
  ?termSpecificPosition
WHERE {
  VALUES (?house ?position) {
    (wd:Q783330 wd:Q6814428)
    (wd:Q783401 wd:Q18912794)
    (wd:Q2867120 wd:Q19007285)
    (wd:Q2994118 wd:Q19627913)
    (wd:Q2994121 wd:Q19299542)
    (wd:Q3244676 wd:Q18810377)
    (wd:Q3365548 rdf:nil)
    (wd:Q4386693 wd:Q18534408)
    (wd:Q494915 rdf:nil)
    (wd:Q6518214 wd:Q18526194)
    (wd:Q7138951 rdf:nil)
    (wd:Q7566117 wd:Q18220900)
    (wd:Q7566128 wd:Q18662245)
    (wd:Q7987530 wd:Q20165902)
    (wd:Q9302011 wd:Q19185341)
    (wd:Q9390783 wd:Q19202748)
    (wd:Q9390785 wd:Q26998278)
  }
  ?house (p:P361/ps:P361)* ?legislature .
      ?baseTerm p:P31|p:P279 [ ps:P279|ps:P31 wd:Q15238777 ; pq:P642 ?legislature ] .
      OPTIONAL { ?subTerm wdt:P31 ?baseTerm }

  BIND(COALESCE(?subTerm, ?baseTerm) AS ?term)

  OPTIONAL { ?term (wdt:P580|wdt:P571) ?termStart. }
  OPTIONAL { ?term (wdt:P582|wdt:P576) ?termEnd. }
  OPTIONAL { ?term (wdt:P155|wdt:P1365) ?termReplaces }
  OPTIONAL { ?term (wdt:P156|wdt:P1366) ?termReplacedBy }
  OPTIONAL {
    ?termSpecificPosition wdt:P31/wdt:P279* wd:Q4164871 ;
                          p:P279 [ ps:P279 ?position ;
                                   pq:P2937 ?term ] .
  }

  FILTER (!BOUND(?termEnd) || ?termEnd > NOW())
  FILTER (!BOUND(?termReplacedBy))
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
} ORDER BY ?termStart ?term
